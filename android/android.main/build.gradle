apply plugin: 'com.android.application'

android {

  dexOptions {
    javaMaxHeapSize "4g"
    jumboMode true
  }

  lintOptions {
    abortOnError false
  }

  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
  }

  compileSdkVersion external.android.sdkVersion
  buildToolsVersion external.android.buildToolsVersion

  defaultConfig {
    minSdkVersion external.android.minSdkVersion
    targetSdkVersion external.android.sdkVersion
    applicationId "me.sneer"
    versionCode 5
    versionName "0.1.1"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
    // Please replace this 6+ with a specific number:
  compile 'com.google.android.gms:play-services-base:6+' //For GCM (Google Cloud Messaging)
  compile('com.netflix.rxjava:rxjava-android:' + external.rx.version) {
    exclude group: 'com.netflix.rxjava'
  }
  compile project(':sneer-android-api')
}

def skummetArtifactsDir = '../../skummet-artifacts'
if (file("$skummetArtifactsDir/me.sneer/core").exists()) {
  println "skummet jar detected!"
  dependencies {
    compile 'me.sneer:core:+:skummet'
  }
  repositories {
    ivy { url skummetArtifactsDir }
  }
  repositories.remove(
    repositories.getByName('MavenLocal'))
} else {
  dependencies {
    compile 'me.sneer:core:+'
  }
}