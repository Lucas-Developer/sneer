version = "0.1.5"

apply plugin: "clojure"
apply plugin: "java"
apply plugin: "maven"

sourceCompatibility = 1.6
targetCompatibility = 1.6

buildscript {
  repositories {
    maven { url "https://clojars.org/repo" }
  }

  dependencies {
    classpath "clojuresque:clojuresque:1.7.0"
  }
}

repositories {
  maven { url "https://clojars.org/repo" }
}

buildscript {
  repositories {
    maven { url 'https://repo.spring.io/plugins-release' }
  }

  dependencies {
    classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
  }
}

apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-eclipse'

clojure.warnOnReflection = true

sourceSets {
  main {
    clojure {
      aotCompile = true
    }
  }
}

compileClojure.mustRunAfter compileJava

dependencies {
  compile 'org.clojure:clojure:1.7.0-alpha5'
  compile 'org.clojure:core.async:0.1.346.0-17112a-alpha'
  compile external['clojure.core.match']
  compile('com.cognitect:transit-clj:0.8.269') {
    exclude group: 'org.msgpack', module: 'msgpack'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    exclude group: 'org.apache.directory.studio', module: 'org.apache.commons.codec'
  }
  compile project(':crypto')
  compile project(':sneer-java-api')
  testCompile 'midje:midje:1.6.3'
  compile 'org.clojure:java.jdbc:0.3.6'
  optional 'org.xerial:sqlite-jdbc:3.8.6'
}


class Lein extends Exec {
  static def onWindows = System.properties['os.name'].toLowerCase().contains('windows')
  Lein() {
    executable (onWindows ? 'lein.bat' : 'lein')
  }
}

task leinSkummet(type: Lein) {
  inputs.source sourceSets.main.clojure
  inputs.source 'project.clj'
  outputs.dir 'target'
  args 'with-profile', 'lean', 'do', 'skummet', 'compile', ',', 'skummet', 'jar'
}

def skummetBuildDir = "$buildDir/skummet"

task skummetJar(type: Copy, dependsOn: leinSkummet) {
  from('target') {
    include "core-$version-standalone.jar"
  }
  into skummetBuildDir
  rename { fileName ->
    fileName.replace("standalone", "skummet")
  }
}

task leinClean(type: Lein) {
  args 'clean'
}

clean.dependsOn leinClean

apply plugin: 'ivy-publish'

publishing {
  repositories {
    ivy { url '../skummet-artifacts' }
  }
  publications {
    skummet(IvyPublication) {
      artifact(file("$skummetBuildDir/core-${version}-skummet.jar")) {
        classifier 'skummet'
        builtBy skummetJar
      }
    }
  }
}

uploadArchives {
  repositories {
    mavenLocal()
  }
}

